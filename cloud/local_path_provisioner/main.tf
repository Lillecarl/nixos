variable "paths" { type = map(string) }
variable "deploy" { type = bool }
locals {
  ids-this-stage0 = data.kustomization_overlay.this.ids_prio[0]
  ids-this-stage1 = var.deploy ? data.kustomization_overlay.this.ids_prio[1] : []
  ids-this-stage2 = var.deploy ? data.kustomization_overlay.this.ids_prio[2] : []
  helm_values = {
    storageClass = {
      create            = true
      defaultClass      = true
      provisionerName   = "rancher.io/local-path"
      defaultVolumeType = "local"
    }
    nodePathMap = [{
      node  = "DEFAULT_PATH_FOR_NON_LISTED_NODES"
      paths = ["/var/lib/rancher/k3s/storage"]
    }]
    configmap = {
      setup = <<YAML
#!/bin/sh
set -eu
mkdir -m 0777 -p "$VOL_DIR"
chmod 700 "$VOL_DIR/.."
YAML
    }
  }
}
data "kustomization_overlay" "this" {
  resources = [for file in tolist(fileset(path.module, "*.yaml")) : "${path.module}/${file}"]
  helm_charts {
    name          = "local-path-provisioner"
    release_name  = "local-path-provisioner"
    namespace     = "lpp-csi"
    include_crds  = true
    values_inline = yamlencode(local.helm_values)
  }
  helm_globals {
    chart_home = var.paths.charts
  }
  kustomize_options {
    load_restrictor = "none"
    enable_helm     = true
    helm_path       = var.paths.helm-path
  }
}
resource "kubectl_manifest" "stage0" {
  for_each   = local.ids-this-stage0
  yaml_body  = data.kustomization_overlay.this.manifests[each.value]
  depends_on = []

  force_conflicts   = true
  apply_only        = true
  server_side_apply = true
  wait              = true
  timeouts { create = "1m" }
}
resource "kubectl_manifest" "stage1" {
  for_each   = local.ids-this-stage1
  yaml_body  = data.kustomization_overlay.this.manifests[each.value]
  depends_on = [kubectl_manifest.stage0]

  force_conflicts   = true
  server_side_apply = true
  wait              = true
  timeouts { create = "1m" }
}
resource "kubectl_manifest" "stage2" {
  for_each   = local.ids-this-stage2
  yaml_body  = data.kustomization_overlay.this.manifests[each.value]
  depends_on = [kubectl_manifest.stage1]

  force_conflicts   = true
  server_side_apply = true
  wait              = true
  timeouts { create = "1m" }
}
